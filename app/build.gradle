apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "com.net.wrappy.im"
        minSdkVersion 22
        targetSdkVersion 28
        versionCode 1
        versionName "0.9.79"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            buildConfigField "String", "SERVER_URL_CORE_SERVICE", "\"https://wrappy-test.newsupplytech.com/\""
            buildConfigField "String", "SERVER_URL_OPENFIRE", "\"im-test.newsupplytech.com\""
            buildConfigField "String", "SERVER_URL_OPENFIRE_MUC", "\"@conference.im-test.newsupplytech.com\""
        }
        release {
            buildConfigField "String", "SERVER_URL_CORE_SERVICE", "\"https://webserv-ci8.wrappy.network/\""
            buildConfigField "String", "SERVER_URL_OPENFIRE", "\"im8.wrappy.network\""
            buildConfigField "String", "SERVER_URL_OPENFIRE_MUC", "\"@conference.im8.wrappy.network\""
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/DEPENDENCIES'
    }
}

configurations {
    all {
        exclude group: 'xpp3', module: 'xpp3'
        exclude module: 'httpclient'
        exclude module: 'commons-logging'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    def room_version = "1.1.1"
    def lifecycle_version = "1.1.1"
    def paging_version = "1.0.1"
    def support_version = '28.0.0'

    // Support Library
    implementation "com.android.support:support-v4:$support_version"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support.constraint:constraint-layout:1.1.2"

    // Room
    implementation "android.arch.persistence.room:runtime:$room_version"
    annotationProcessor "android.arch.persistence.room:compiler:$room_version"

    // Lifecycle and ViewModel
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"

    // Paging
    implementation "android.arch.paging:runtime:$paging_version"

    // Dagger 2
    implementation 'com.google.dagger:dagger:2.16'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.16'

    // RetroFit
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.4'

    // OkHttp
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'

    // CropIwa
    implementation 'com.steelkiwi:cropiwa:1.0.3'

    // View Pager Indicator
    implementation 'com.romandanylyk:pageindicatorview:1.0.1@aar'

    //QR Code Scanner and Generator
    implementation 'com.google.zxing:core:3.3.2'
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0@aar'

    //XMPP
    implementation 'org.igniterealtime.smack:smack-android:4.3.0'
    implementation 'org.igniterealtime.smack:smack-extensions:4.3.0'
    implementation 'org.igniterealtime.smack:smack-experimental:4.3.0'
    implementation 'org.igniterealtime.smack:smack-tcp:4.3.0'

    //ChatKit
    implementation 'com.github.stfalcon:chatkit:0.3.0'

    // Phone Number Verification (Country Code Picker)
    implementation 'com.hbb20:ccp:2.2.2'

    // SwipeMenu
    implementation 'com.tubb.smrv:swipemenu-recyclerview:5.4.7'

    // Pattern Cipher
    implementation 'com.itsxtt:patternlockview:0.1.0'

    implementation 'me.piruin:quickaction:2.4.2'

    // Google Play Services Location
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.android.gms:play-services-maps:16.1.0'

    // Firebase
    implementation 'com.google.firebase:firebase-core:16.0.7'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'

    // AWS
    implementation 'com.amazonaws:aws-android-sdk-s3:2.7.+'
    implementation ('com.amazonaws:aws-android-sdk-mobile-client:2.7.+@aar') { transitive = true }
    implementation ('com.amazonaws:aws-android-sdk-auth-userpools:2.7.+@aar') { transitive = true }
    
    // AudioGram AudioWave
    implementation 'com.github.alxrm:audiowave-progressbar:0.9.2'
    // TrueTime
    implementation 'com.github.instacart.truetime-android:library:3.4'

    // Google Cloud Translate
    implementation 'com.google.cloud:google-cloud-translate:1.77.0'

    // Subsampling Image View
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'

    implementation 'com.github.Str4tocaster:otr4j:custom-SNAPSHOT'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation ('org.jitsi.react:jitsi-meet-sdk:2.1.0') { transitive = true }
}

apply plugin: 'com.google.gms.google-services'